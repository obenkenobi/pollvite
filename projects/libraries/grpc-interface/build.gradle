buildscript {
    ext {
        protobufPluginVersion = '0.9.3'
    }
}

plugins {
    id 'com.google.protobuf' version "${protobufPluginVersion}"
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
}

group 'com.pollvite'
version = '0.0.1-SNAPSHOT'

jar {
    archivesBaseName = 'grpc-interface'
}

dependencies {
    implementation mavenDeps.grpcProtobuff
    implementation mavenDeps.grpcStub
    implementation mavenDeps.reactorGrpcStub
    compileOnly mavenDeps.springBootReactorNetty // Required to compile against reactor
    compileOnly mavenDeps.javaxAnnotationApi // Workaround since spring boot 3 does not use javax
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
}

protobuf {
    protoc {
        artifact = mavenDeps.protobufProtoc
    }
    generatedFilesBaseDir = "$projectDir/src/generated"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        java_grpc {
            artifact = mavenDeps.protocGenGrpcJava
        }
        java_reactor {
            artifact = mavenDeps.reactorGrpc
        }
    }
    generateProtoTasks {

        all().each { task ->
            task.plugins {
                java_grpc {}
                java_reactor {}
            }
            task.builtins {
                java {}
            }
        }
    }
}

idea {
    module {
        sourceDirs += file("src/generated/main/java")
        sourceDirs += file("src/generated/main/java_grpc")
        sourceDirs += file("src/generated/main/java_reactor")
        generatedSourceDirs += file("src/generated/main/java")
        generatedSourceDirs += file("src/generated/main/java_grpc")
        generatedSourceDirs += file("src/generated/main/java_reactor")

    }
}

repositories {
    mavenCentral()
}